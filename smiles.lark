smiles: sequence+
sequence: atom ( union | branch | gap )*
union: bond? ( bridge | sequence )
branch: "(" ( dot | bond )? sequence ")" ( union | branch )
gap: dot sequence
atom: star | shortcut | selection | bracket
bracket: "[" isotope? symbol parity? virtual_hydrogen? charge? "]"
isotope: nonzero digit? digit?
symbol: star | element | selection
virtual_hydrogen: "H" nonzero?
charge: ( "+" | "-" ) nonzero?
bridge: nonzero | "%" nonzero digit
parity: "@" "@"?
star: "*"
dot: "."
shortcut: "B" | "Br" | "C" | "Cl" | "N" | "O" | "P" | "S" | "F" | "I" | "Sc" | "Sn"
selection: "b" | "c" | "n" | "o" | "p" | "s" | "se" | "as" | "te"
element: "Ac" | "Ag" | "Al" | "Am" | "Ar" | "As" | "At" | "Au"
                    | "B" | "Ba" | "Be" | "Bi" | "Bk" | "Br"
                    | "C" | "Ca" | "Cd" | "Ce" | "Cf" | "Cl" | "Cm" | "Co"
                    | "Cr" | "Cs" | "Cu"
                    | "Dy"
                    | "Er" | "Es" | "Eu"
                    | "F" | "Fe" | "Fm" | "Fr"
                    | "Ga" | "Gd" | "Ge"
                    | "H" | "He" | "Hf" | "Hg" | "Ho"
                    | "I" | "In" | "Ir"
                    | "K" | "Kr"
                    | "La" | "Li" | "Lr" | "Lu"
                    | "Mg" | "Mn" | "Mo"
                    | "N" | "Na" | "Nb" | "Nd" | "Ne" | "Ni" | "No" | "Np"
                    | "O" | "Os"
                    | "P" | "Pa" | "Pb" | "Pd" | "Pm" | "Po" | "Pr" | "Pt" | "Pu"
                    | "Ra" | "Rb" | "Re" | "Rf" | "Rh" | "Rn" | "Ru"
                    | "S" | "Sb" | "Sc" | "Se" | "Si" | "Sm" | "Sn" | "Sr"
                    | "Ta" | "Tb" | "Tc" | "Te" | "Th" | "Ti" | "Tl" | "Tm"
                    | "U" | "V" | "W" | "Xe" | "Y" | "Yb"
                    | "Zn" | "Zr"
bond: "-" | "=" | "#" | "/" | "\\" | ":"
digit: "0" | nonzero
nonzero: "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
